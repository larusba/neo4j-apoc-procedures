Make sure to set the config options in your `neo4j.conf`

.Neo4j.conf

----
apoc.export.file.enabled=true
----

// tag::export.json[]
`YIELD file, source, format, nodes, relationships, properties, time, rows`
[cols="1m,5"]
|===
| apoc.export.json.query(query,file,config) | exports results from the Cypher statement as Json to the provided file
| apoc.export.json.all(file,config) | exports whole database as Json to the provided file
| apoc.export.json.data(nodes,rels,file,config) | exports given nodes and relationships as Json to the provided file
| apoc.export.json.graph(graph,file,config) | exports given graph object as Json to the provided file
|===
// end::export.json[]

.Config

|===
| useTypes | true/false, if true export datatypes too.
|===


== Examples

For the examples below we create the following dataset:

[source,cypher]
----
CREATE (f:User {name:'Adam',age:42,male:true,kids:['Sam','Anna','Grace'], born:localdatetime('2015185T19:32:24'), place:point({latitude: 13.1, longitude: 33.46789})})-[:KNOWS {since: 1993}]->(b:User {name:'Jim',age:42}),(c:User {age:12})
----

.Export query

[source,cypher]
----
CALL apoc.export.json.query("MATCH (u:User) WHERE age > {age} return u","/tmp/query.json",{params:{age:10}})
----

result:

[source,json]
----
include::data/exportJSON/query_nodes_param.json[leveloffset]
----

.Export query Complex

[source,cypher]
----
CALL apoc.export.json.query("RETURN {value:1, data:[10,'car',null, point({ longitude: 56.7, latitude: 12.78 }), point({ longitude: 56.7, latitude: 12.78, height: 8 }), point({ x: 2.3, y: 4.5 }), point({ x: 2.3, y: 4.5, z: 2 }),date('2018-10-10'), datetime('2018-10-18T14:21:40.004Z'), localdatetime({ year:1984, week:10, dayOfWeek:3, hour:12, minute:31, second:14, millisecond: 645 }), {x:1, y:[1,2,3,{age:10}]}]} as key","/tmp/complex.json")
----

result:

[source,json]
----
include::data/exportJSON/MapComplex.json[leveloffset]
----

.Export queryList

[source,cypher]
----
CALL apoc.export.json.query("MATCH (u:User) RETURN COLLECT(u) as list","/tmp/list.json",{params:{age:10}})
----

result:

[source,json]
----
include::data/exportJSON/listNode.json[leveloffset]
----

.Export queryMap

[source,cypher]
----
CALL apoc.export.json.query("MATCH (u:User)-[r:KNOWS]->(d:User) RETURN u {.*}, d {.*}, r {.*}","/tmp/map.json",{params:{age:10}})
----

result:

[source,json]
----
include::data/exportJSON/MapNode.json[leveloffset]
----

.Export all

[source,cypher]
----
CALL apoc.export.json.all("/tmp/all.json",{useTypes:true})
----

result:

[source,json]
----
include::data/exportJSON/all.json[leveloffset]
----

.Export graph

[source,cypher]
----
CALL apoc.graph.fromDB('test',{}) yield graph
CALL apoc.export.json.graph(graph,"tmp/graph.json",{})
YIELD nodes, relationships, properties, file, source,format, time
RETURN *
----

result:

[source,json]
----
include::data/exportJSON/graph.json[leveloffset]
----

.Export data

[source,cypher]
----
MATCH (nod:User)
MATCH ()-[rels:KNOWS]->()
WITH collect(nod) as a, collect(rels) as b
CALL apoc.export.json.data(a, b, "tmp/data.json", null)
YIELD nodes, relationships, properties, file, source,format, time
RETURN *
----

result:

[source,json]
----
include::data/exportJSON/data.json[leveloffset]
----
